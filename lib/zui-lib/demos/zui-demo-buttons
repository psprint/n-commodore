# Started from Zle or from command line
#
# Shows all types of hyperlinks, from raw-links to internal-inline links

-zui_glib_cleanup deserialize:"zui-demo-buttons"
-zui_glib_init app:"zui-demo-buttons" app_name:"ZUI Buttons"
emulate -LR zsh -o extendedglob -o typesetsilent -o warncreateglobal
-zui_glib_init2 # after emulate -LR

-zui_glib_store_default_app_config b:border 1
-zui_glib_store_default_app_config b:status_border 1
-zui_glib_store_default_app_config s:status_size 5
-zui_glib_store_default_app_config s:text_mode off

local internal  # trick variable to make a handler no-restart
local -a logs   # own logs queue

# Generator for module 1
demo_generator_A() {
    local mod="$1" ice="$2" arg="$3"

    [[ -n "$arg" ]] && logs+=( "Regenerated with: $arg" )

    reply=()
    -zui_glib_rc_button_ext "raw_link" "" "" ",mod1_ice1," "raw-link" "${ZUI[MAGENTA]}link1${ZUI[FMT_END]}"
    -zui_glib_rc_button_ext "external" "$mod" "$ice" "external action" "" "${ZUI[MAGENTA]}link2${ZUI[FMT_END]}" demo_external_action
    -zui_glib_rc_button "external_inline" "${ZUI[MAGENTA]}link3${ZUI[FMT_END]}" 'reply=( ",mod1_ice1," "inline handler (external)" )'
    -zui_glib_rc_button_ext "internal" "$mod" "$ice" "internal action" "" "${ZUI[MAGENTA]}link4${ZUI[FMT_END]}" demo_internal_action # has "internal" in name
    -zui_glib_rc_button "internal_inline" "${ZUI[MAGENTA]}link5${ZUI[FMT_END]}" 'internal=1; -zui_glib_fly_mod_regen "'"$mod"'" "'"$ice"'" "inline handler (internal)"'

    # Content
    reply=(
        "RESTART: Raw-link: ${reply[1]}, hyperlink with external action: ${reply[2]}, with inline (external) code: ${reply[3]}"
        "NO-RESTART: Hyperlink with internal action: ${reply[4]}, with inline (internal) code: ${reply[5]}"
        "Random number: $RANDOM"
    )

    # Non-selectable lines  Hops to jump with [ and ]   Local anchors
    reply2=( 3 )            reply3=( 1 )                reply4=( )
}

demo_external_action() {
    local id="$1" mod="$2" ice="$3" data3="$4" data4="$4"
    # Request regeneration, pass on data3 as REGENERATE_ARG
    reply=( ",mod${mod}_ice${ice}," "$data3" )
}

demo_internal_action() {
    local id="$1" mod="$2" ice="$3" data3="$4" data4="$4"
    -zui_glib_fly_mod_regen "$mod" "$ice" "$data3"
}

-zui-standard-status-callback() {
    [[ "${#logs}" -gt 0 ]] && { reply=( "Message: " "${logs[1]}" ); logs=( ${(@)logs[2,-1]} ); return 1; }
    # Resulting type: 0 - no message
    return 0
}

## Start application ##
zui-event-loop 1:demo_generator_A

-zui_glib_cleanup serialize

return 0

# vim:ft=zsh
